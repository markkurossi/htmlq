Program = { TopLevelClause, ';'};

TopLevelClause = ( VariableDecl
	         | VariableInit
		 | PrintStmt
		 | SelectClause );


VariableDecl = 'DECLARE', Identifier, Type;
VariableInit = 'SET', Identifier, '=', Expr;

PrintStmt = 'PRINT', Expr;

SelectClause = 'SELECT', SelectColumns,
	       [ 'INTO', Identifier ],
	       [ 'FROM', FromClause, { ',', FromClause } ],
	       [ 'WHERE', Expr ];

SelectColumns = SelectColumn, {',', SelectColumn};
SelectColumn = Expr, [ AsClause ];

FromClause = (String, [ 'FILTER', String ] | '(', SelectClause, ')'),
	     'AS', Identifier;

Expr = LogicalAndExpr, {'OR', LogicalAndExpr};

LogicalAndExpr = LogicalNotExpr, {'AND', LogicalNotExpr};

LogicalNotExpr = ['NOT'], ComparativeExpr;

ComparativeExpr = AdditiveExpr,
		  {('=' | '<>' | '<' | '<=' | '>' | '>='), AdditiveExpr};

AdditiveExpr = MultiplicativeExpr, {('+' | '-'), MultiplicativeExpr};

MultiplicativeExpr = UnaryExpr, {('*' | '/'), UnaryExpr};

UnaryExpr = PostfixExpr;

PostfixExpr = '(', Expr, ')'
	    | SimpleReference
	    | QualifiedReference
	    | FunctionCall
	    | Bool
	    | integer
	    | real
	    | String
	    ;

SimpleReference = Identifier;
QualifiedReference = Identifier, '.', Identifier;
FunctionCall = Identifier, '(', {Arguments}, ')';

Arguments = Expr, {',', Expr};

AsClause = 'AS', Identifier;

Bool = 'TRUE' | 'FALSE';

Identifier = ucLetter, {ucLetter|ucDigit|'_'|'$'} | '"', characters, '"';

String = "'", characters, "'";

Type = 'BOOLEAN' | 'INTEGER' | 'REAL' | 'VARCHAR';
